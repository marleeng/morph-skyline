#Q8: List All routes that pass through a specific stop
#eg: vig.q08.STOPID=0000000000000000003o
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gtfs: <http://vocab.gtfs.org/terms#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX gtfsstop: <http://transport.linkeddata.es/madrid/metro/stops/>

SELECT * WHERE {
	?route a gtfs:Route .
	OPTIONAL { ?route gtfs:shortName ?routeShortName . }
	OPTIONAL { ?route dct:description ?routeDescription . }

	?trip a gtfs:Trip .
	OPTIONAL { ?trip gtfs:shortName ?tripShortName . }
	?trip gtfs:service ?service .
	?trip gtfs:route ?route .

	?stopTime a gtfs:StopTime . 
	?stopTime gtfs:trip ?trip . 
	?stopTime gtfs:stop ?stop . 

	?stop a gtfs:Stop . 
	OPTIONAL {?stop dct:description ?stopDescription . } .
        FILTER (?stop=gtfsstop:0000000000000000003o)
}
